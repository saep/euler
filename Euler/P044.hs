{- |
Module      :  Euler.P044
Description :  Problem 044
Copyright   :  (c) Sebastian Witte
License     :  WTFPL

Maintainer  :  woozletoff@gmail.com
Stability   :  experimental
Portability :  portable

Pentagon numbers

Pentagonal numbers are generated by the formula, Pn=n(3n−1)/2. The
first ten pentagonal numbers are:

1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...

It can be seen that P(4) + P(7) = 22 + 70 = 92 = P(8). However, their
difference, 70 − 22 = 48, is not pentagonal.

Find the pair of pentagonal numbers, P(j) and P(k), for which their
sum and difference are pentagonal and D = |P(k) − P(j)| is minimised;
what is the value of D?

-}
module Euler.P044
       ( solve
       ) where

import Control.Monad
import Data.Maybe
import Data.Monoid
import Data.IntSet (IntSet, member, insert)
import Euler.Numbers (pentagonal)

solve :: IO ()
solve = print solution

solution :: Int
solution = uncurry (subtract) $ findPair mempty [] [1..]

findPair :: IntSet -> [Int] -> [Int] -> (Int, Int)
findPair m ps (z:zs) =
    let pz = pentagonal z
        m' = insert pz m
    in fromMaybe (findPair m' (pz:ps) zs) $ findD m' ps pz

-- p(z) = p(j) + p(k) <=> p(j) = p(z) - p(k)
findD :: IntSet -> [Int] -> Int -> Maybe (Int, Int)
findD _ [] _ = Nothing
findD m (pk:pks) pz
    | 2*pk - pz < 0 = Nothing
    | (2*pk - pz) `member` m && (pz - pk) `member` m = Just (pz - pk, pk)
    | otherwise = findD m pks pz

-- p(z) - p(k) = p(j)
-- p(k) - p(j) = p(k) - p(z) + p(k) = 2p(k) - p(z)


